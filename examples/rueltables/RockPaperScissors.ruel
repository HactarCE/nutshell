@RUEL RockPaperScissors
From "RockScissorsPaperLife" by Dave Greene

@TABLE
states: 4
neighborhood: Moore
symmetries: permute

# Dead cells for State 1
deadA = {0, 2, 3}
# Dead cells for State 2
deadB = {0, 1, 3}
# Dead cells for State 3
deadC = {0, 1, 2}

# Birth in competitive circumstances
{0, 1}, N..E 1, SE..SW 3, deadC, deadC, 3
{0, 2}, N..E 2, SE..SW 1, deadA, deadA, 1
{0, 3}, N..E 3, SE..SW 2, deadB, deadB, 2

# Birth
{0, 1}, N..E 3, SE..NW deadC, 3
{0, 2}, N..E 1, SE..NW deadA, 1
{0, 3}, N..E 2, SE..NW deadB, 2

# the RockScissorsPaper rules
{1..3}, [0: {3, 1, 2}], NE..NW any, 0

# Three-neighbor survival
# these can't be moved into the Birth transitions above, because
# 'the RockScissorsPaper rules' have to override them
1, N..E 1, SE..NW deadA, 1
2, N..E 2, SE..NW deadB, 2
3, N..E 3, SE..NW deadC, 3

# Two-neighbor survival
1, N..NE 1, E..NW deadA, 1
2, N..NE 2, E..NW deadB, 2
3, N..NE 3, E..NW deadC, 3

# Death
any, N..NW any, 0